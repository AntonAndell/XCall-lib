/*
 * Copyright (c) 2022-2022 Balanced.network.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import dependencies.Dependencies

version = '0.1.0'

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    compileOnly Dependencies.javaeeApi
    implementation Dependencies.javaeeScorex
    implementation Dependencies.minimalJson
    implementation Dependencies.javaeeTokens
    implementation project(':score-lib')
    implementation project(':SpokeToken')

    compileOnly Dependencies.javaeeScoreClient
    annotationProcessor Dependencies.javaeeScoreClient

    // testImplementation "org.web3j:web3j-unit:4.9.6"
    testImplementation project(':test-lib')
    testImplementation Dependencies.javaeeUnitTest
    testImplementation Dependencies.javaeeTokens
    testImplementation Dependencies.mockitoCore
    testImplementation Dependencies.mockitoInline
    // Use JUnit Jupiter for testing.
    testImplementation Dependencies.junitJupiter
    testRuntimeOnly Dependencies.junitJupiterEngine
}

// web3j {
//     generatedPackageName = "xcall.sample.hubtoken"
// }

optimizedJar {
    mainClassName = 'xcall.sample.hubtoken.HubTokenBase'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

deployJar {
    endpoints {
        sejong {
            uri = 'https://sejong.net.solidwallet.io/api/v3'
            nid = 0x53
        }
        berlin {
            uri = 'https://berlin.net.solidwallet.io/api/v3'
            nid = 0x7
            to = "cx47323a98cc44f146356f50e77422a0106922bf29"
        }
        lisbon {
            uri = 'https://lisbon.net.solidwallet.io/api/v3'
            nid = 0x2
        }
        local {
            uri = 'http://localhost:9082/api/v3'
            nid = 0x3
        }
        mainnet {
            uri = 'https://ctz.solidwallet.io/api/v3'
            nid = 0x1
        }
    }
    keystore = rootProject.hasProperty('keystoreName') ? "$keystoreName" : ''
    password = rootProject.hasProperty('keystorePass') ? "$keystorePass" : ''

    parameters {
        arg("_nid", "0x7.icon")
        arg("_tokenName", "HubToken")
        arg("_symbolName", "HUBT")
        arg("_decimals", "18")
        arg("_initialSupply", "1000000000000000000000000")
    }
}

tasks.named('test') {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}
